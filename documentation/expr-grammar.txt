expression 		::= assignment-expression
assignment-expression	::= conditional-expression
			  | unary-expression assignment-operator
			    assignment-expression

assignment-operator	::= '='

conditional-expression	::= logical-OR-expression
			  | logical-OR-expression '?' expression : conditional-expression

logical-OR-expression	::= logical-AND-expression
			  | logical-OR-expression '||' logical-AND-expression

logical-AND-expression	::= inclusive-OR-expression
			  | logical-AND-expression '&&' inclusive-OR-expression

inclusive-OR-expression	::= exclusive-OR-expression
exclusive-OR-expression	::= AND-expression
AND-expression		::= equality-expression

equality-expression	::= relational-expression
			  | equality-expression '==' relational-expression
			  | equality-expression '!=' relational-expression

relational-expression	::= shift-expression
			  | relational-expression '<' shift-expression

shift-expression	::= additive-expression

additive-expression	::= multiplicative-expression
			  | additive-expression '+' mulplicative-expression
			  | additive-expression '-' mulplicative-expression

mulplicative-expression	::= cast-expression
			  | mulplicative-expression '*' cast-expression

cast-expression		::= unary-expression

unary-expression	::= postfix-expression
			  | unary-operator cast-expression
			  | 'sizeof' unary-expression
			  | 'sizeof' '(' type-name ')'

unary-operator		::= '&' | '*' | '-' | '!'

postfix-expression	::= primary-expression
			  | postfix-expression '[' expression ']'
			  | postfix-expression '(' argument-expr-list? ')'
			  | postfix-expression '.' identifier
			  | postfix-expression '->' identifier

argument-expr-list	::= assignment-expression
			  | argument-expr-list ',' assignment-expression

primary-expression	::= identifier
			  | constant
			  | string-literal
			  | '(' expression ')'
