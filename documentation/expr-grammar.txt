expression 		::= assignment-expression
			  | expression ',' assignment-expression

assignment-expression	::= conditional-expression
			  | unary-expression assignment-operator
			    assignment-expression

assignment-operator	::= '=' | '*=' | '/=' | '%=' | '+=' | '-=' | '<<=' | '>>='
			  | '&=' | '^=' | '|='

conditional-expression	::= logical-OR-expression
			  | logical-OR-expression '?' expression : conditional-expression

logical-OR-expression	::= logical-AND-expression
			  | logical-OR-expression '||' logical-AND-expression

logical-AND-expression	::= inclusive-OR-expression
			  | logical-AND-expression '&&' inclusive-OR-expression

inclusive-OR-expression	::= exclusive-OR-expression
			  | inclusive-OR-expression '|' exclusive-OR-expression

exclusive-OR-expression	::= AND-expression
			  | exclusive-OR-expression '^' AND-expression

AND-expression		::= equality-expression
			  | AND-expression '&' equality-expression

equality-expression	::= relational-expression
			  | equality-expression '==' relational-expression
			  | equality-expression '!=' relational-expression

relational-expression	::= shift-expression
			  | relational-expression '<' shift-expression
			  | relational-expression '>' shift-expression
			  | relational-expression '<=' shift-expression
			  | relational-expression '>=' shift-expression

shift-expression	::= additive-expression
			  | shift-expression '<<' additive-expression
			  | shift-expression '>>' additive-expression

additive-expression	::= multiplicative-expression
			  | additive-expression '+' mulplicative-expression
			  | additive-expression '-' mulplicative-expression

mulplicative-expression	::= cast-expression
			  | mulplicative-expression '*' cast-expression
			  | mulplicative-expression '/' cast-expression
			  | mulplicative-expression '%' cast-expression

cast-expression		::= unary-expression
			  | '(' type-name ')' cast-expression

unary-expression	::= postfix-expression
			  | '++' unary-expression
			  | '--' unary-expression
			  | unary-operator cast-expression
			  | 'sizeof' unary-expression
			  | 'sizeof' '(' type-name ')'
			  | '_Alignof' '(' type-name ')'

unary-operator		::= '&' | '*' | '+' | '-' | '~' | '!'

postfix-expression	::= primary-expression
			  | postfix-expression '[' expression ']'
			  | postfix-expression '(' argument-expr-list? ')'
			  | postfix-expression '.' identifier
			  | postfix-expression '->' identifier
			  | postfix-expression '++'
			  | postfix-expression '--'
			  | '(' type-name ')' '{' initializer-list '}'
			  | '(' type-name ')' '{' initializer-list ',' '}'

argument-expr-list	::= assignment-expression
			  | argument-expr-list ',' assignment-expression

primary-expression	::= identifier
			  | constant
			  | string-literal
			  | '(' expression ')'

NOTE: Incomplete, because generic-selection is ignored for now.