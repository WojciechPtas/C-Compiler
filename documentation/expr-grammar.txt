assignment-expression	::= conditional-expression
			  | unary-expression '=' assignment-expression

conditional-expression	::= logical-OR-expression
			  | logical-OR-expression '?' assignment-expression ':'
			    conditional-expression

logical-OR-expression	::= logical-AND-expression
			  | logical-OR-expression '||' logical-AND-expression

logical-AND-expression	::= equality-expression
			  | logical-AND-expression '&&' equality-expression

equality-expression	::= relational-expression
			  | equality-expression '==' relational-expression
			  | equality-expression '!=' relational-expression

relational-expression	::= additive-expression
			  | relational-expression '<' additive-expression

additive-expression	::= multiplicative-expression
			  | additive-expression '+' mulplicative-expression
			  | additive-expression '-' mulplicative-expression

mulplicative-expression	::= unary-expression
			  | mulplicative-expression '*' unary-expression

unary-expression	::= postfix-expression
			  | unary-operator unary-expression
			  | 'sizeof' unary-expression
			  | 'sizeof' '(' type-name ')'

unary-operator		::= '&' | '*' | '-' | '!'

postfix-expression	::= primary-expression
			  | postfix-expression '[' assignment-expression ']'
			  | postfix-expression '(' argument-expr-list? ')'
			  | postfix-expression '.' identifier
			  | postfix-expression '->' identifier

argument-expr-list	::= assignment-expression
			  | argument-expr-list ',' assignment-expression

primary-expression	::= identifier
			  | constant
			  | string-literal
			  | '(' assignment-expression ')'
